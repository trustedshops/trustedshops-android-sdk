apply plugin: 'com.android.library'
ext {
    bintrayRepo = 'maven'
    bintrayName = 'trustedshops-android-sdk'

    publishedGroupId = 'com.trustedshops.androidsdk'
    libraryName = 'Trusted Shops SDK'
    artifact = 'trustedshops-android-sdk'

    libraryDescription = 'Trusted Shops SDK for Android'

    siteUrl = 'https://github.com/trustedshops/trustedshops-android-sdk'
    gitUrl = 'https://github.com/trustedshops/trustedshops-android-sdk.git'

    libraryVersion = '1.7.0'

    developerId = 'trustedshops'
    developerName = 'Timur Olzhabayev'
    developerEmail = 'timur.olzhabayev@trustedshops.com'

    licenseName = 'The MIT License (MIT)'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}
android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 0
        versionName libraryVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.squareup.okhttp3:okhttp:3.1.2'
    compile 'com.afollestad.material-dialogs:core:0.8.+'
    compile 'org.apache.directory.studio:org.apache.commons.codec:1.8'
    testCompile 'org.apache.directory.studio:org.apache.commons.io:2.4'
    testCompile 'org.json:json:20160212'
}

apply plugin: 'com.github.dcendents.android-maven'

group = publishedGroupId                               // Maven Group ID for the artifact

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact

                // Add your description here
                name libraryName
                description libraryDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

version = libraryVersion

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}


apply plugin: 'com.jfrog.bintray'

// Bintray
//Properties properties = new Properties()
//properties.load(project.rootProject.file('local.properties').newDataInputStream())

def bintrayUser = hasProperty("bintray.user") ? getProperty("bintray.user") : System.getenv("bintray.user")
def bintrayApiKey = hasProperty("bintray.apikey") ? getProperty("bintray.apikey") : System.getenv("bintray.apikey")
def bintrayOrg = hasProperty("bintray.org") ? getProperty("bintray.org") : System.getenv("bintray.org")
bintray {
    user = bintrayUser
    key = bintrayApiKey

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        userOrg = bintrayOrg
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = false
        version {
            desc = libraryDescription
            gpg {
                sign = false //Determines whether to GPG sign the files. The default is false
            }
        }
    }
}
